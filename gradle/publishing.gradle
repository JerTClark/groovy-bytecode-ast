/*
 * Copyright 2014 the original author or authors.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *      http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// configuration of the Maven artifacts
apply plugin: 'maven-publish'

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def Node root = asNode()

                root.appendNode('name', '@Bytecode AST transformation')
                root.appendNode('description', 'Allows writing bytecode directly as a method body in Groovy classes')
                root.appendNode('url', 'https://github.com/melix/groovy-bytecode-ast')

                def issues = root.appendNode( 'issueManagement' )
                issues.appendNode( 'system', 'github' )
                issues.appendNode( 'url', 'https://github.com/melix/groovy-bytecode-ast/issues' )

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', 'https://github.com/melix/groovy-bytecode-ast' )
                scm.appendNode( 'connection', 'scm:git:https://github.com/melix/groovy-bytecode-ast.git' )
                scm.appendNode( 'developerConnection', 'scm:git:git@github.com:melix/groovy-bytecode-ast.git' )

                def license = root.appendNode( 'licenses' ).appendNode( 'license' );
                license.appendNode( 'name', 'The Apache Software License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt' )
                license.appendNode( 'distribution', 'repo' )

                def dev = root.appendNode( 'developers' ).appendNode( 'developer' );
                dev.appendNode( 'id', 'melix' )
                dev.appendNode( 'name', 'CÃ©dric Champeau' )
                dev.appendNode( 'organization', 'Groovy' )
                dev.appendNode( 'organizationUrl', 'http://groovy-lang.org' )

            }
        }
    }
}
